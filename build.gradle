plugins {
    id 'java'
    id 'io.qameta.allure' version '2.11.2'
    id "io.freefair.lombok" version "8.6"
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

def aspectjWeaverVersion = "1.9.22",
    selenideVersion = "6.13.0",
    restAssuredVersion = "5.3.0",
    allureVersion = "2.27.0",
    junitVersion = "5.9.3",
    slf4jVersion = "2.0.7",
    ownerVersion = "1.0.4"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencies {
    testImplementation(
            "org.aspectj:aspectjweaver:$aspectjWeaverVersion",
            "com.codeborne:selenide:$selenideVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.qameta.allure:allure-rest-assured:$allureVersion",
            "io.appium:java-client:8.3.0",
            'org.assertj:assertj-core:3.6.1',
            "io.rest-assured:rest-assured:$restAssuredVersion",
            "io.rest-assured:json-schema-validator:$restAssuredVersion",
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "org.aeonbits.owner:owner:$ownerVersion",
            "commons-io:commons-io:2.11.0"
    )
}

tasks.withType(Test) {
    systemProperties(System.getProperties())
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task ui_tests(type: Test) {
    useJUnitPlatform {
        includeTags("UI")
    }
}

task api_tests(type: Test) {
    useJUnitPlatform {
        includeTags("API")
    }
}

task mobile_tests(type: Test) {
    useJUnitPlatform {
        includeTags("Mobile")
    }
}

task all_tests(type: Test) {
    useJUnitPlatform {
        includeTags("UI", "API", "Mobile")
    }
}

